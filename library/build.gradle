plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

//apply from: "../publish.gradle"

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation project(path: ":flutter")
}

def group = "com.worktile.flutter"
def _artifact = "nestedscrollview"
def ver = "1.0.0-SNAPSHOT"

project.afterEvaluate {
    project.task([type: Jar], "sourceJar") {
        if (project.extensions.findByName("android")) {
            from project.extensions["android"].sourceSets.main.java.srcDirs
        } else {
            from project.sourceSets.main.java.srcDirs
        }
    }

    project.publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                if (project.components.findByName("release")) {
                    from project.components.release
                } else {
                    from project.components.java
                }

                artifact project.sourceJar {
                    classifier "sources"
                }

                // You can then customize attributes of the publication as shown below.
                groupId = group
                artifactId = _artifact
                version = ver
            }

            debug(MavenPublication) {
                // Applies the component for the release build variant.
                if (project.components.findByName("debug")) {
                    from project.components.debug
                } else {
                    from project.components.java
                }

                artifact project.sourceJar {
                    classifier "sources"
                }

                // You can then customize attributes of the publication as shown below.
                groupId = group
                artifactId = _artifact
                version = ver
            }
        }

        repositories {
            maven {
                name "worktile"
                url "https://maven.worktile-android.com/repository/worktile/"
                credentials {
                    it.username = MAVEN_USERNAME
                    it.password = MAVEN_PASSWORD
                }
            }

            maven {
                name "debug"
                url "https://maven.worktile-android.com/repository/debug/"
                credentials {
                    it.username = MAVEN_USERNAME
                    it.password = MAVEN_PASSWORD
                }
            }
        }
    }
}